sp {pickup-block*propose*grasp-block
   (state <s> ^name pickup-block
              ^quiescence t
              ^block-id <id>
              ^top-state.parameters <param>
              ^io.input-link <input>)
   (<param> ^pickup-range <pr>
            ^pickup-angle-off-pos <paop>
            ^pickup-angle-off-neg <paon>)
   (<input> -^self.carry.id
             ^object <o>)
   (<o> ^type block
        ^visible yes
        ^range < <pr>
        ^id <id>
        ^angle-off.yaw { < <paop> > <paon>})
-->
   (<s> ^operator <op> + =)
   (<op> ^name grasp-block
         ^actions <a>)
   (<a> ^get.id <id>
        ^rotate.direction stop
        ^move.direction stop)
}

sp {apply*grasp-block*remove-block-from-blocks
   (state <s> ^operator.name grasp-block
              ^top-state.blocks <b>
              ^block-id <id>)
   (<b> ^block <block>)
   (<block> ^id <id>)
-->
   (<b> ^block <block> -)
}
