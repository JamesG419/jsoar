#echo "\nLoading all/record-explore-item"

## This records the location of items in the map during exploration.
## It it not used in wander/attack/...

sp {all*propose*record-explore-item
   (state <s> ^name << forward-to-door follow-room-axis follow-hall-axis goto-door >>
              ^top-state.map <map>)
   (<map> ^item <item>
          ^explore-room <er>)
   (<item> -^room
            ^x <x>
            ^y <y>)
   (<er> ^wall <w1> <w2> <w3> <w4>)
   (<w1> ^side west ^x < <x>)
   (<w2> ^side east ^x > <x>)
   (<w3> ^side south ^y < <y>)
   (<w4> ^side north ^y > <y>)
   -->
   (<s> ^operator <o> + =, >)
   (<o> ^name record-explore-item
        ^item <item>
        ^room <er>)}

sp {all*apply*record-explore-item
   (state <s> ^operator <o>)
   (<o> ^name record-explore-item
        ^item <item>
        ^room <er>)
   -->
   (<item> ^room <er>)
   (<er> ^item <item>)}
 

