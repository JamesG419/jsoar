sp {move-directly-to-gateway*propose*turn-around-object
   (state <s> ^name move-directly-to-gateway
              ^quiescence t
              ^io.input-link.object <obj>
              ^object-gateway-diff <ogd>)
   (<obj> ^id <id>
          ^angle-off.yaw { <ao> > 0 })
   (<ogd> ^obstacle yes
         -^against-wall
          ^id <id>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name turn-around-object
         ^actions.rotate-relative.yaw (- <ao> 1.0)
         ^actions.move.direction forward)
}

sp {move-directly-to-gateway*propose*turn-around-object-neg
   (state <s> ^name move-directly-to-gateway
              ^io.input-link.object <obj>
              ^object-gateway-diff <ogd>)
   (<obj> ^id <id>
          ^angle-off.yaw { <ao> <= 0 })
   (<ogd> ^obstacle yes
         -^against-wall 
          ^id <id>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name turn-around-object
         ^actions.rotate-relative.yaw (+ <ao> 1.0)
         ^actions.move.direction forward)
}

sp {move-directly-to-gateway*propose*turn-around-object*wall
   (state <s> ^name move-directly-to-gateway
              ^quiescence t
              ^io.input-link.object <obj>
              ^top-state <ts>
              ^object-gateway-diff <ogd>)
   (<ts> ^direction { <> <new-dir> <dir> }
         ^directions <dirs>)
   (<dirs> ^opposite <wall-dir>
           ^value <new-dir>
           ^heading <heading>)
   (<ogd> ^obstacle yes
          ^against-wall <wall-dir>)
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name turn-around-object
         ^actions.rotate-absolute.yaw <heading>
         ^actions.move.direction forward)
}
