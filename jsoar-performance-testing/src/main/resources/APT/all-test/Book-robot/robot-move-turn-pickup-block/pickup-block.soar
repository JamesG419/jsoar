sp {robot-pickup-block*propose*pickup-block
   (state <s> ^name robot
              ^parameters <param>
              ^io.input-link <input>)
   (<param> ^pickup-range <pr>
            ^pickup-angle-off-pos <paop>
            ^pickup-angle-off-neg <paon>)
   (<input> -^self.carry.id
            ^object <o>)
   (<o> ^type block
        ^visible yes
        ^range < <pr>
        ^id <id>
        ^angle-off { < <paop> > <paon>})
-->
   (<s> ^operator <op> + =, >)
   (<op> ^name pickup-block
         ^block-id <id>)
}

sp {apply*pickup-block
   (state <s> ^operator <op>
              ^io.output-link <out>)
   (<op> ^name pickup-block
         ^block-id <id>)
-->
   (<out> ^get.id <id>
          ^rotate.direction stop
          ^move.direction stop)
}
