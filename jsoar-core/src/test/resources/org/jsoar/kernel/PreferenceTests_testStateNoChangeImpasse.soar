# test for state no-change impasse
# makes sure that the right wmes are created and no others
# this means the test will need to be updated when support for additional modules is added
# if those modules add new links to substates

sp {report*state-no-change*impasse
#make sure all wmes that should be here are here and of the correct type
   (state <s> ^attribute state
              ^choices none
              ^impasse no-change
              ^quiescence t
              ^type state
              ^reward-link {<rl> <=> <s>}
              ^smem {<sm> <=> <s>}
              ^epmem {<em> <=> <s>}
              ^superstate {<ss> <=> <s>})
#make sure we're at the right level in the goal stack
    (<ss> ^superstate nil)
#make sure there are no wmes with unexpected attributes
   -(<s> ^{<> attribute 
           <> choices
           <> impasse
           <> quiescence
           <> type
           <> reward-link
           <> smem
           <> epmem
           <> superstate})
#make sure there are no wmes with unexpected values
    (<s> -^attribute <> state
         -^choices <> none
         -^impasse <> no-change
         -^quiescence <> t
         -^type <> state
         -^reward-link <> <rl>
         -^smem <> <sm>
         -^epmem <> <em>
         -^superstate <> <ss>)
-->
   (succeeded)
   (write (crlf) |correct state no-change impasse detected|)
}