# this test confirms that the GDS will not be triggered if a wme in the GDS has support from multiple productions, 
#   and one of those productions unmatches (but the wme stays in WM) 
# create an intermediate on the top state from an operator application
# create another wme on the top state twice from two elaboration rules (one dependent upon the first wme)
# go into a substate
# create an o-supported wme in the substate that depends on the doubly-supported wme
# remove the top-state intermediate wme (causing one of the two productions supporting the GDS wme to go away)
# confirm that the substate is still there (the GDS was not triggered)

# create the top-state wme (^in gds)
sp {propose*init
(state <s> ^superstate nil
          -^init done)
-->
(<s> ^operator.name init)
}

sp {apply*init
(state <s> ^operator.name init)
-->
(<s> ^intermediate wme
     ^init done)
}

sp {support*in-gds*direct
(state <s> ^superstate nil)
-->
(<s> ^in gds)
}

sp {support*in-gds*indirect
(state <s> ^intermediate wme)
-->
(<s> ^in gds)
}


# create substate. Note that it only depends on ^init done, because we want to
# make sure that the state goes away because ^in gds is in the GDS, not because
# this proposal unmatched
sp {propose*create-substate
(state <s> ^init done)
-->
(<s> ^operator.name create-substate)
}

# create wme (^put-in gds) in substate that depends on ^in gds
sp {propose*create-put-in-gds
(state <s> ^superstate <ss>
-^put-in gds)
(<ss> ^operator.name create-substate
^in gds)
-->
(<s> ^operator.name create-put-in-gds)
}

sp {apply*create-put-in-gds
(state <s> ^operator.name create-put-in-gds)
-->
(<s> ^put-in gds)
}

# trigger GDS state removal by removing ^in gds
sp {propose*trigger-gds
(state <s> ^superstate <ss>
^put-in gds)
(<ss> ^operator.name create-substate
^in gds)
-->
(<s> ^operator.name trigger-gds)
}

sp {apply*trigger-gds
(state <s> ^operator.name trigger-gds
^superstate <ss>)
(<ss> ^intermediate wme)
-->
(<ss> ^intermediate wme -)
}

# test that state was removed when ^in-gds was removed
sp {success
(state <s> ^init done
-^intermediate wme)
(state <ss> ^superstate <s>)
-->
(write (crlf) |gds not triggered|)
(succeeded)
}