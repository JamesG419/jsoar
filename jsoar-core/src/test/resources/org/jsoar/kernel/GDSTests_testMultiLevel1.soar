### -*- Mode: SDE -*-
###############################################################################
###
### File              : multilevel1.soar
###
### Original author(s): Robert E. Wray
### Organization      : University of Michigan AI Lab
### Created on        : 28 Feb 1997, 09:18:42
### Last Modified By  : IFOR <ifor@pelican.eecs.umich.edu>
### Last Modified On  : 07 Mar 1997, 13:46:17
### Soar Version      : 6
###
### Description       : simple test case for OPERAND2:
###
###                     operator returns o-supported results to both a
###                     non-top-goal superstate and also makes o-supported
###                     changes to the local state.  The purpose of this
###                     example is to show what that the GDS gets instantiated
###                     for both the local o-supported WME and the supergoal
###                     result.
###############################################################################

sp {elaborate-state*top-state-pointer*init
    (state <s> ^superstate nil)
    -->
    (<s> ^top-state <s>)}

sp {elaborate-state*top-state-pointer
    (state <s> ^superstate.top-state <ts>)
    -->
    (<s> ^top-state <ts>)}


sp {elaborate-state*problem-space*top
    (state <s> ^superstate nil)
    -->
    (<s> ^problem-space <p>)
    (<p> ^name top)}

sp {top*elaborate*a*b*c
   (state <s>           ^problem-space <ps>)
   (<ps> ^name top)
   -->
   (<s> ^a a
    ^b b
    ^c c)}

sp {top*propose*operator-1
    (state <s> ^problem-space <p>)
    (<p> ^name top)
    -->
    (<s> ^operator <o> + )
    (<o> ^name operator-1)}


### Results get returned to this 'second' state

sp {elaborate-state*problem-space*second
    (state <s> ^superstate <ss>
           ^attribute operator
           ^impasse no-change)
    (<ss> ^problem-space <ps>)
    (<ps> ^name top)
    -->
    (<s> ^problem-space <p>)
    (<p> ^name second)}

sp {second*elaborate*d
   (state <s> ^superstate <ss>
          ^problem-space <ps>)
   (<ss> ^a <a> ^b <b>)
   (<ps> ^name second)
   -->
   (<s> ^d d)}

sp {second*elaborate*e
   (state <s> ^superstate <ss>
          ^problem-space <ps>)
   (<ss> ^c <c>)
   (<ps> ^name second)
   -->
   (<s> ^e e)}
   

sp {top-2*propose*operator-2
    (state <s> ^problem-space <p>)
    (<p> ^name second)
    -->
    (<s> ^operator <o> + )
    (<o> ^name operator-2)}


### Results get returned *from* this state

sp {elaborate-state*problem-space*third
    (state <s> ^superstate <ss>
           ^impasse no-change)
    (<ss> ^problem-space <ps>            ^operator <o>)
    (<ps> ^name second)
   (<o> ^name operator-2)
    -->
    (<s> ^problem-space <p>)
    (<p> ^name third)}

sp {third*elaborate*f
   (state <s> ^superstate <ss>
          ^problem-space <ps>)
   (<ss> ^d <a> ^e <b>)
   (<ps> ^name third)
   -->
   (<s> ^f f)}

sp {third*propose*operator-3
    (state <s> ^problem-space <p>)
    (<p> ^name third)
    -->
    (<s> ^operator <o> + )
    (<o> ^name operator-3)}

sp {operator-3*apply*local-structure-and-result
    (state <s> ^operator.name operator-3
           ^f <f>
           ^superstate <ss>)
    -->
    (<ss> ^supergoal-result <f>)
       (<s> ^local-result local)
}

sp {succeeded
(state <s> ^local-result <local>
^superstate.supergoal-result)
-->
(halt)
}