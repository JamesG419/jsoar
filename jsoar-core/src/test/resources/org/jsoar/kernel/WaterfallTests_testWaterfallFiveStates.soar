# Test case for revised waterfall model
#
# <production>-<inner_e cycle it fires in>-><target state>
#
# S1: P1-1->S1
# S2: P2-2->S1 P3-1->S2
# S3: P4-3->S1 P5-3->S2 P6-1->S3
# S4: 
# S5: P7-4->S1 P8-4->S2 P9-4->S3 P10-1->S4 P11-1->S5

chunk --off

# create s2
sp {propose*test1
(state <s> ^superstate nil)
-->
(<s> ^operator <o> +)
(<o> ^name test1)
(<s> ^level1 <s>)
}

# create s3
sp {propose*test2
(state <s> ^superstate <ss>)
(<ss> ^operator.name test1)
-->
(<s> ^operator <o> +)
(<o> ^name test2)
(<s> ^level1 <ss>
     ^level2 <s>)
}

# create s4
sp {propose*test3
(state <s> ^superstate <ss>)
(<ss> ^operator.name test2
      ^superstate <sss>)
-->
(<s> ^operator <o> +)
(<o> ^name test3)
(<s> ^level1 <sss>
     ^level2 <ss>
     ^level3 <s>)
}

# create s5
sp {propose*test4
(state <s> ^superstate <ss>)
(<ss> ^operator.name test3
      ^superstate <sss>)
(<sss> ^superstate <ssss>)
-->
(<s> ^operator <o> +)
(<o> ^name test4)
(<s> ^level1 <ssss>
     ^level2 <sss>
     ^level3 <ss>
     ^level4 <s>)
}

# elaborate levels for s5, trigger test productions to all fire
sp {states*created*go
(state <s> ^superstate <ss>)
(<ss> ^operator.name test4
      ^superstate <sss>)
(<sss> ^superstate <ssss>)
(<ssss> ^superstate <sssss>)
-->
(<s> ^level1 <sssss>
     ^level2 <ssss>
     ^level3 <sss>
     ^level4 <ss>
     ^level5 <s>)
(<sssss> ^go true)
(write (crlf) |Let's go!| (crlf))
}

# S1: P1-1->S1
sp {test*production*1
(state <s> ^level1.go true)
(<s> ^superstate nil)
(<s> ^level1 <s1>)
-->
(<s1> ^touched-by p1)
(write (crlf) |S1: P1-1->S1| (crlf))
}

# S2: P2-2->S1
sp {test*production*2
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test1)
(<s> ^level1 <s1>)
-->
(<s1> ^touched-by p2)
(write (crlf) |S2: P2-2->S1| (crlf))
}

# S2: P3-1->S2
sp {test*production*3
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test1)
(<s> ^level2 <s2>)
-->
(<s2> ^touched-by p3)
(write (crlf) |S2: P3-1->S2| (crlf))
}

# S3: P4-3->S1 
sp {test*production*4
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test2)
(<s> ^level1 <s1>)
-->
(<s1> ^touched-by p4)
(write (crlf) |S3: P4-3->S1| (crlf))
}

# S3: P5-3->S2 
sp {test*production*5
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test2)
(<s> ^level2 <s2>)
-->
(<s2> ^touched-by p5)
(write (crlf) |S3: P5-3->S2| (crlf))
}

# S3: P6-1->S3
sp {test*production*6
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test2)
(<s> ^level3 <s3>)
-->
(<s3> ^touched-by p6)
(write (crlf) |S3: P6-1->S3| (crlf))
}

# S5: P7-4->S1 
sp {test*production*7
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test4)
(<s> ^level1 <s1>)
-->
(<s1> ^touched-by p7)
(write (crlf) |S5: P7-4->S1| (crlf))
}

# S5: P8-4->S2 
sp {test*production*8
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test4)
(<s> ^level2 <s2>)
-->
(<s2> ^touched-by p8)
(write (crlf) |S5: P8-4->S2| (crlf))
}

# S5: P9-4->S3 
sp {test*production*9
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test4)
(<s> ^level3 <s3>)
-->
(<s3> ^touched-by p9)
(write (crlf) |S5: P9-4->S3| (crlf))
}

# S5: P10-1->S4 
sp {test*production*10
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test4)
(<s> ^level4 <s4>)
-->
(<s4> ^touched-by p10)
(write (crlf) |S5: P10-1->S4| (crlf))
}

# S5: P11-1->S5
sp {test*production*11
(state <s> ^level1.go true)
(<s> ^superstate.operator.name test4)
(<s> ^level5 <s5>)
-->
(<s5> ^touched-by p11)
(write (crlf) |S5: P11-1->S5| (crlf))
}

# halt after all fired
# S1: P1-1->S1
# S2: P2-2->S1 P3-1->S2
# S3: P4-3->S1 P5-3->S2 P6-1->S3
# S4: 
# S5: P7-4->S1 P8-4->S2 P9-4->S3 P10-1->S4 P11-1->S5
sp {halt*production
(state <s> ^level1.go true)
(<s> ^level1 <s1>
     ^level2 <s2>
     ^level3 <s3>
     ^level4 <s4>
     ^level5 <s5>)
(<s1> ^touched-by p1
      ^touched-by p2
      ^touched-by p4
      ^touched-by p7)
(<s2> ^touched-by p3
      ^touched-by p5
      ^touched-by p8)
(<s3> ^touched-by p6
      ^touched-by p9)
(<s4> ^touched-by p10)
(<s5> ^touched-by p11)
-->
(succeeded)
}
