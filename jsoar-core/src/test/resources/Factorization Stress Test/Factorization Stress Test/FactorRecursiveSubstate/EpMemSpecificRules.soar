# This file contains all the EpMem specific rules
# To enable these rules, source in FactorizationStressTest_EpMem

####################################################
###                 OPERATORS                    ###
####################################################

####################################################
###           LOOK-UP-IN-EPMEM-FACTORS           ###
# This looks up a factor in EpMem.
sp {propose*look-up-in-epmem-factors
   (state <s> ^name factor-recursive-substate
              ^using-epmem true
             -^needs-factorization
             -^factorization-object <fo>
             -^epmem.command.query)
-->
   (<s> ^operator <o> + >)
   (<o> ^name look-up-in-epmem-factors)
}

# This actually creates the query
sp {apply*look-up-in-epmem-factors
   (state <s> ^operator.name look-up-in-epmem-factors
              ^original-number-to-factor <n>
              ^epmem.command <ec>)
-->
   (<ec> ^query.factorization-object.number <n>)
}

####################################################
###            FOUND-FACTORS-IN-EPMEM            ###
# If we found some factors, this will be proposed
# and called to get the factors.
sp {propose*found-factors-in-epmem
   (state <s> ^name factor-recursive-substate
              ^using-epmem true
              ^epmem.result.retrieved <episode>
              ^epmem.result.success
              ^original-number-to-factor <n>)
   (<episode> ^factorization-object.number <n>)
 -{(state <s> ^factorization-object <fo>)
   (<fo> ^epmem true)}
-->
   (<s> ^operator <o> + >)
   (<o> ^name found-factors-in-epmem)
}

# This copies only the factor we want from an episode
sp {apply*found-factors-in-epmem
   (state <s> ^operator.name found-factors-in-epmem
              ^epmem.result.retrieved <episode>
              ^original-number-to-factor <n>)
   (<episode> ^factorization-object <fo>)
   (<fo> ^number <n>)
-->
   (<s> ^factorization-object <fo>)
   (<fo> ^epmem true)
}

####################################################
###       COPY-FACTORIZATION-OBJECTS*EPMEM       ###
# This copies all the factorization objects to the superstate
# since this substate is about to be blown away.
sp {apply*copy-factorization-objects*epmem
   (state <s> ^operator.name copy-factorization-objects
			  ^factorization-object <fo>
     		  ^original-number-to-factor <n>
     		  ^superstate <ss>)
   (<fo> ^complete true
         ^number <> <n>
         ^epmem true)
-->
   (<ss> ^factorization-object <fo>)
}

####################################################
###     GENERIC-OPERATOR*ADD-IS-EPMEM-OBJECT     ###
# This piggy backs onto other operators.  All this
# does is add an o-supported epmem flag to any
# factorization object created in an epmem substate.
# This is necessary so epmem objects always propogate
# to the top state.
sp {apply*generic-operator*add-is-epmem-object
   (state <s> ^name factor-recursive-substate
              ^using-epmem true
              ^operator <o>
              ^factorization-object <fo>
              ^original-number-to-factor <n>)
   (<fo> ^number <n>
        -^epmem)
-->
   (<fo> ^epmem true)
}
