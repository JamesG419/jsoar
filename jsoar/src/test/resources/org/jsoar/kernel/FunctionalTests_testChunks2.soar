# Used by testChunks2 and testChunks2WithLearning. In the latter, learning
# is turned on in the unit test before the file is loaded.

# 0: create an ONC so we get o-supported results

sp {propose*osupporter
(state <s> ^superstate nil)
-->
(<s> ^operator.name osupporter)
}

sp {elaborate*state*name
  (state <s> ^superstate.operator.name <name>)
-->
  (<s> ^name <name>)
}

# 1: SNC test

sp {snc-result
   (state <s> ^impasse no-change
              ^type state
              ^superstate.superstate <ss>)
   (<ss> ^operator.name osupporter
        -^snc result)
-->
   (<ss> ^snc result)
}

# 2: operator tie test

sp {propose*one
(state <s> ^superstate.snc result
           ^name osupporter)
-->
(<s> ^operator.name one)
}

sp {propose*two
(state <s> ^superstate.snc result
           ^name osupporter)
-->
(<s> ^operator.name two)
}

sp {tie-result
   (state <s> ^impasse tie
              ^superstate.superstate <ss>)
   (<ss> ^operator.name osupporter
        -^tie result)
-->
   (<ss> ^tie result)
}

# 3: operator conflict test

sp {force-conflict
(state <s> ^superstate.tie result
           ^name osupporter
           ^operator <o1> + <o2> +)
(<o1> ^name one)
(<o2> ^name two)
-->
(<s> ^operator <o1> > <o2>)
(<s> ^operator <o2> > <o1>)
}

sp {conflict-result
   (state <s> ^impasse conflict
              ^superstate.superstate <ss>)
   (<ss> ^operator.name osupporter
        -^conflict result)
-->
   (<ss> ^conflict result)
}

# 4: constraint-failure test

sp {resolve-conflict
(state <s> ^superstate.conflict result
           ^name osupporter
           ^operator <o1> + <o2> +)
(<o1> ^name one)
(<o2> ^name two)
-->
(<s> ^operator <o1> -)
(<s> ^operator <o2> -)
}

sp {propose*three
(state <s> ^superstate.conflict result
           ^name osupporter)
-->
(<s> ^operator <o> !~)
(<o> ^name three)
}

sp {constraint-failure-result
   (state <s> ^impasse constraint-failure
              ^superstate.superstate <ss>)
   (<ss> ^operator.name osupporter
        -^cf result)
-->
   (<ss> ^cf result)
}

sp {success
(state <s> ^snc result
           ^tie result
		   ^conflict result
		   ^cf result)
-->
(write (crlf) |Success!|)
(succeeded)
}
